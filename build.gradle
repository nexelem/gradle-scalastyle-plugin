/*
 * Copyright 2013. Muhammad Ashraf
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */
apply plugin: "java"
apply plugin: "scala"
apply plugin: "groovy"
apply plugin: 'signing' // For pushing to Sonatype OSS Staging repository
apply plugin: 'bintray'
apply plugin: 'maven-publish'

version = '0.4.1'
group = "org.github.mansur.scalastyle"

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.scalastyle:scalastyle_2.10:0.4.0'
    compile 'org.scala-lang:scala-library:2.10'
    testCompile 'junit:junit:4.11'
}

compileGroovy.dependsOn(compileScala)

// Required for Sonatype
task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier "javadoc"
    extension "jar"
}

configurations.create("sources")
configurations.create("javadoc")
configurations.archives {
    extendsFrom configurations.sources
    extendsFrom configurations.javadoc
}

artifacts {
    sources(sourcesJar) {
        type "jar"
    }
    javadoc(javadocJar) {
        type "javadoc"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

bintray {        
    user = project.hasProperty('user') ? project.user : ''
    key = project.hasProperty('key') ? project.key : ''
    publications = ['mavenJava']
    pkg {
        repo = 'release'
        userOrg = 'releashaus'
        name = 'gradle-scalastyle-plugin'
        desc = 'gradle-scalastyle is a Gradle plugin for running the Scalastyle style checker.'
        licenses = ['Apache-2.0']
        labels = ['v4.1']
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.11"
}
